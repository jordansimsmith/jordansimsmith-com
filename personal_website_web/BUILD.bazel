load("@aspect_rules_js//js:defs.bzl", "js_library", "js_run_binary")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//:vite/package_json.bzl", "bin")

npm_link_all_packages(
    name = "node_modules",
)

js_library(
    name = "vite-config",
    srcs = ["vite.config.js"],
    deps = [
        "//:node_modules/@vitejs/plugin-react",
        "//:node_modules/vite",
    ],
)

js_library(
    name = "postcss-config",
    srcs = ["postcss.config.js"],
    deps = [
        "//:node_modules/postcss-preset-mantine",
        "//:node_modules/postcss-simple-vars",
    ],
)

bin.vite_binary(
    name = "vite",
    chdir = package_name(),
    data = [":vite-config"],
)

js_library(
    name = "assets",
    srcs = glob([
        "src/**/*.css",
        "src/**/*.svg",
    ]),
)

js_library(
    name = "src",
    srcs = glob([
        "src/**/*.js",
        "src/**/*.jsx",
    ]),
    data = [":assets"],
    deps = [
        "//:node_modules/@mantine/core",
        "//:node_modules/react",
        "//:node_modules/react-dom",
    ],
)

filegroup(
    name = "public",
    srcs = glob(["public/**/*"]),
)

js_run_binary(
    name = "build",
    srcs = [
        "index.html",
        "package.json",
        ":assets",
        ":postcss-config",
        ":public",
        ":src",
    ],
    args = ["build"],
    out_dirs = ["dist"],
    tool = ":vite",
)

bin.vite_binary(
    name = "preview",
    args = ["preview"],
    chdir = package_name(),
    data = [":build"],
)
