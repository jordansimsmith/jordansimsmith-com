load("@contrib_rules_jvm//java:defs.bzl", "JUNIT5_DEPS", "java_test_suite")
load("@rules_java//java:defs.bzl", "java_binary", "java_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

HANDLERS = glob(["src/main/java/**/*Handler.java"])

INTEGRATION_TESTS = glob(["src/test/java/**/*IntegrationTest.java"])

E2E_TESTS = glob(["src/test/java/**/*E2ETest.java"])

UNIT_TESTS = glob(
    ["src/test/java/**/*Test.java"],
    exclude = INTEGRATION_TESTS + E2E_TESTS,
)

java_library(
    name = "lib",
    srcs = glob(
        ["src/main/java/**/*.java"],
        exclude = HANDLERS,
    ),
    visibility = [
        "//visibility:public",
    ],
    deps = [
        "//lib/dynamodb:lib",
        "//lib/json:lib",
        "//lib/time:lib",
        "//third_party/dagger",
        "@maven//:com_fasterxml_jackson_core_jackson_annotations",
        "@maven//:com_fasterxml_jackson_core_jackson_databind",
        "@maven//:com_google_guava_guava",
        "@maven//:net_sf_biweekly_biweekly",
        "@maven//:org_jsoup_jsoup",
        "@maven//:software_amazon_awssdk_dynamodb",
        "@maven//:software_amazon_awssdk_dynamodb_enhanced",
    ],
)

java_binary(
    name = "get-calendar-subscription-handler",
    srcs = ["src/main/java/com/jordansimsmith/footballcalendar/GetCalendarSubscriptionHandler.java"],
    create_executable = False,
    resources = [
        "src/main/resources/logback.xml",
    ],
    deps = [
        ":lib",
        "//third_party/dagger",
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:ch_qos_logback_logback_core",
        "@maven//:com_amazonaws_aws_lambda_java_core",
        "@maven//:com_amazonaws_aws_lambda_java_events",
        "@maven//:com_google_guava_guava",
        "@maven//:net_sf_biweekly_biweekly",
        "@maven//:org_slf4j_slf4j_api",
        "@maven//:software_amazon_awssdk_dynamodb_enhanced",
    ],
)

java_binary(
    name = "update-fixtures-handler",
    srcs = ["src/main/java/com/jordansimsmith/footballcalendar/UpdateFixturesHandler.java"],
    create_executable = False,
    resources = [
        "src/main/resources/logback.xml",
    ],
    deps = [
        ":lib",
        "//lib/time:lib",
        "//third_party/dagger",
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:ch_qos_logback_logback_core",
        "@maven//:com_amazonaws_aws_lambda_java_core",
        "@maven//:com_amazonaws_aws_lambda_java_events",
        "@maven//:com_google_guava_guava",
        "@maven//:org_slf4j_slf4j_api",
        "@maven//:software_amazon_awssdk_dynamodb_enhanced",
    ],
)

java_binary(
    name = "comet-mock-server",
    srcs = ["src/test/java/com/jordansimsmith/footballcalendar/CometMockServer.java"],
    create_executable = False,
)

java_binary(
    name = "football-fix-mock-server",
    srcs = ["src/test/java/com/jordansimsmith/footballcalendar/FootballFixMockServer.java"],
    create_executable = False,
)

java_binary(
    name = "subfootball-mock-server",
    srcs = ["src/test/java/com/jordansimsmith/footballcalendar/SubfootballMockServer.java"],
    create_executable = False,
)

java_library(
    name = "test-lib",
    testonly = True,
    srcs = glob(
        ["src/test/java/**/*.java"],
        exclude = UNIT_TESTS + INTEGRATION_TESTS + E2E_TESTS,
    ),
    data = [
        ":comet-mock-load",
        ":football-fix-mock-load",
        ":local-load",
        ":subfootball-mock-load",
    ],
    resources = [
        "src/test/resources/logback-test.xml",
        "src/test/resources/test.properties",
    ],
    deps = [
        ":lib",
        "//lib/dynamodb:test-lib",
        "//lib/http:test-lib",
        "//lib/json:lib",
        "//lib/localstack:lib",
        "//lib/testcontainers:test-lib",
        "//lib/time:test-lib",
        "//third_party/dagger",
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:ch_qos_logback_logback_core",
        "@maven//:com_amazonaws_aws_lambda_java_core",
        "@maven//:com_amazonaws_aws_lambda_java_events",
        "@maven//:com_google_guava_guava",
        "@maven//:javax_inject_javax_inject",
        "@maven//:org_slf4j_slf4j_api",
        "@maven//:org_testcontainers_junit_jupiter",
        "@maven//:org_testcontainers_testcontainers",
        "@maven//:software_amazon_awssdk_dynamodb",
        "@maven//:software_amazon_awssdk_dynamodb_enhanced",
    ],
)

java_test_suite(
    name = "unit-tests",
    size = "small",
    srcs = UNIT_TESTS,
    runner = "junit5",
    runtime_deps = JUNIT5_DEPS,
    deps = [
        ":lib",
        ":test-lib",
        "@maven//:com_fasterxml_jackson_core_jackson_databind",
        "@maven//:net_sf_biweekly_biweekly",
        "@maven//:org_assertj_assertj_core",
        "@maven//:org_jsoup_jsoup",
        "@maven//:org_junit_jupiter_junit_jupiter_api",
        "@maven//:org_mockito_mockito_core",
    ],
)

java_test_suite(
    name = "integration-tests",
    size = "medium",
    srcs = INTEGRATION_TESTS,
    env = {
        "AWS_ACCESS_KEY_ID": "fake",
        "AWS_SECRET_ACCESS_KEY": "fake",
        "AWS_REGION": "ap-southeast-2",
    },
    resources = [
        "src/test/resources/logback-test.xml",
    ],
    runner = "junit5",
    test_suffixes = ["IntegrationTest.java"],
    runtime_deps = JUNIT5_DEPS,
    deps = [
        ":get-calendar-subscription-handler",
        ":lib",
        ":test-lib",
        ":update-fixtures-handler",
        "//lib/dynamodb:test-lib",
        "//lib/testcontainers:test-lib",
        "//lib/time:test-lib",
        "//third_party/dagger",
        "@maven//:com_amazonaws_aws_lambda_java_events",
        "@maven//:com_google_guava_guava",
        "@maven//:javax_inject_javax_inject",
        "@maven//:net_sf_biweekly_biweekly",
        "@maven//:org_assertj_assertj_core",
        "@maven//:org_junit_jupiter_junit_jupiter_api",
        "@maven//:org_testcontainers_junit_jupiter",
        "@maven//:software_amazon_awssdk_dynamodb",
        "@maven//:software_amazon_awssdk_dynamodb_enhanced",
    ],
)

pkg_tar(
    name = "local-handlers.tar",
    srcs = [
        ":get-calendar-subscription-handler_deploy.jar",
        ":update-fixtures-handler_deploy.jar",
    ],
    package_dir = "/opt/code/localstack/",
)

pkg_tar(
    name = "local-init-resources.tar",
    srcs = ["src/test/resources/init_resources.py"],
    package_dir = "/etc/localstack/init/ready.d/",
)

oci_image(
    name = "local-image",
    base = "@localstack//:localstack",
    env = {
        "LOCALSTACK_LAMBDA_IGNORE_ARCHITECTURE": 1,
        "LOCALSTACK_LAMBDA_KEEPALIVE_MS": 0,
    },
    tars = [
        ":local-init-resources.tar",
        ":local-handlers.tar",
    ],
)

oci_load(
    name = "local-load",
    image = ":local-image",
    repo_tags = [
        "bazel/football-calendar-local:latest",
    ],
)

pkg_tar(
    name = "comet-mock-server.tar",
    srcs = [
        ":comet-mock-server_deploy.jar",
    ],
    package_dir = "/opt/code/comet-mock/",
)

oci_image(
    name = "comet-mock-image",
    base = "@temurin21jre//:temurin21jre",
    tars = [
        ":comet-mock-server.tar",
    ],
)

oci_load(
    name = "comet-mock-load",
    image = ":comet-mock-image",
    repo_tags = [
        "bazel/football-calendar-comet-mock:latest",
    ],
)

pkg_tar(
    name = "football-fix-mock-server.tar",
    srcs = [
        ":football-fix-mock-server_deploy.jar",
    ],
    package_dir = "/opt/code/football-fix-mock/",
)

oci_image(
    name = "football-fix-mock-image",
    base = "@temurin21jre//:temurin21jre",
    tars = [
        ":football-fix-mock-server.tar",
    ],
)

oci_load(
    name = "football-fix-mock-load",
    image = ":football-fix-mock-image",
    repo_tags = [
        "bazel/football-calendar-football-fix-mock:latest",
    ],
)

pkg_tar(
    name = "subfootball-mock-server.tar",
    srcs = [
        ":subfootball-mock-server_deploy.jar",
    ],
    package_dir = "/opt/code/subfootball-mock/",
)

oci_image(
    name = "subfootball-mock-image",
    base = "@temurin21jre//:temurin21jre",
    tars = [
        ":subfootball-mock-server.tar",
    ],
)

oci_load(
    name = "subfootball-mock-load",
    image = ":subfootball-mock-image",
    repo_tags = [
        "bazel/football-calendar-subfootball-mock:latest",
    ],
)

java_test_suite(
    name = "e2e-tests",
    size = "large",
    srcs = E2E_TESTS,
    env = {
        "AWS_ACCESS_KEY_ID": "fake",
        "AWS_SECRET_ACCESS_KEY": "fake",
        "AWS_REGION": "ap-southeast-2",
    },
    resources = [
        "src/test/resources/logback-test.xml",
    ],
    runner = "junit5",
    test_suffixes = ["E2ETest.java"],
    runtime_deps = JUNIT5_DEPS,
    deps = [
        ":lib",
        ":test-lib",
        "//lib/dynamodb:test-lib",
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:ch_qos_logback_logback_core",
        "@maven//:com_fasterxml_jackson_core_jackson_core",
        "@maven//:com_fasterxml_jackson_core_jackson_databind",
        "@maven//:net_sf_biweekly_biweekly",
        "@maven//:org_assertj_assertj_core",
        "@maven//:org_junit_jupiter_junit_jupiter_api",
        "@maven//:org_slf4j_slf4j_api",
        "@maven//:org_testcontainers_junit_jupiter",
        "@maven//:org_testcontainers_testcontainers",
        "@maven//:software_amazon_awssdk_dynamodb",
        "@maven//:software_amazon_awssdk_dynamodb_enhanced",
        "@maven//:software_amazon_awssdk_lambda",
    ],
)
