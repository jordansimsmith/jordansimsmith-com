load("@contrib_rules_jvm//java:defs.bzl", "JUNIT5_DEPS", "java_test_suite")
load("@rules_java//java:defs.bzl", "java_binary", "java_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

HANDLERS = glob(["src/main/java/**/*Handler.java"])

INTEGRATION_TESTS = glob(["src/test/java/**/*IntegrationTest.java"])

E2E_TESTS = glob(["src/test/java/**/*E2ETest.java"])

java_library(
    name = "lib",
    srcs = glob(
        ["src/main/java/**/*.java"],
        exclude = HANDLERS,
    ),
    deps = [
        "//lib/dynamodb:lib",
        "//lib/notifications:lib",
        "//lib/time:lib",
        "//third_party/dagger",
        "@maven//:com_google_guava_guava",
        "@maven//:org_jsoup_jsoup",
        "@maven//:software_amazon_awssdk_dynamodb",
        "@maven//:software_amazon_awssdk_dynamodb_enhanced",
    ],
)

java_binary(
    name = "update-page-content-handler",
    srcs = glob(["src/main/java/com/jordansimsmith/subfootballtracker/UpdatePageContentHandler.java"]),
    create_executable = False,
    resources = [
        "src/main/resources/logback.xml",
    ],
    deps = [
        ":lib",
        "//lib/notifications:lib",
        "//lib/time:lib",
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:ch_qos_logback_logback_core",
        "@maven//:com_amazonaws_aws_lambda_java_core",
        "@maven//:com_amazonaws_aws_lambda_java_events",
        "@maven//:com_google_code_findbugs_jsr305",
        "@maven//:com_google_guava_guava",
        "@maven//:org_slf4j_slf4j_api",
        "@maven//:software_amazon_awssdk_dynamodb_enhanced",
    ],
)

java_binary(
    name = "subfootball-website-stub-server",
    srcs = ["src/test/java/com/jordansimsmith/subfootballtracker/SubfootballWebsiteStubServer.java"],
    create_executable = False,
)

java_library(
    name = "test-lib",
    testonly = True,
    srcs = glob(
        ["src/test/java/**/*.java"],
        exclude = INTEGRATION_TESTS + E2E_TESTS,
    ),
    data = [
        ":local-load",
        ":subfootball-website-stub-load",
    ],
    resources = [
        "src/test/resources/logback-test.xml",
        "src/test/resources/test.properties",
    ],
    deps = [
        ":lib",
        "//lib/dynamodb:test-lib",
        "//lib/localstack:lib",
        "//lib/notifications:test-lib",
        "//lib/testcontainers:lib",
        "//lib/time:test-lib",
        "//third_party/dagger",
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:ch_qos_logback_logback_core",
        "@maven//:com_google_code_findbugs_jsr305",
        "@maven//:com_google_guava_guava",
        "@maven//:org_slf4j_slf4j_api",
        "@maven//:org_testcontainers_junit_jupiter",
        "@maven//:org_testcontainers_testcontainers",
        "@maven//:software_amazon_awssdk_dynamodb",
        "@maven//:software_amazon_awssdk_dynamodb_enhanced",
    ],
)

java_test_suite(
    name = "integration-tests",
    size = "medium",
    srcs = INTEGRATION_TESTS,
    env = {
        "AWS_ACCESS_KEY_ID": "fake",
        "AWS_SECRET_ACCESS_KEY": "fake",
        "AWS_REGION": "ap-southeast-2",
    },
    resources = [
        "src/test/resources/logback-test.xml",
    ],
    runner = "junit5",
    test_suffixes = ["IntegrationTest.java"],
    runtime_deps = JUNIT5_DEPS,
    deps = [
        ":lib",
        ":test-lib",
        ":update-page-content-handler",
        "//lib/dynamodb:test-lib",
        "//lib/notifications:test-lib",
        "//lib/testcontainers:lib",
        "//lib/time:test-lib",
        "@maven//:com_amazonaws_aws_lambda_java_core",
        "@maven//:com_amazonaws_aws_lambda_java_events",
        "@maven//:org_assertj_assertj_core",
        "@maven//:org_junit_jupiter_junit_jupiter_api",
        "@maven//:org_testcontainers_junit_jupiter",
        "@maven//:software_amazon_awssdk_dynamodb",
        "@maven//:software_amazon_awssdk_dynamodb_enhanced",
    ],
)

pkg_tar(
    name = "local-handlers.tar",
    srcs = [
        ":update-page-content-handler_deploy.jar",
    ],
    package_dir = "/opt/code/localstack/",
)

pkg_tar(
    name = "local-init-resources.tar",
    srcs = ["src/test/resources/init_resources.py"],
    package_dir = "/etc/localstack/init/ready.d/",
)

oci_image(
    name = "local-image",
    base = "@localstack//:localstack",
    env = {
        "LOCALSTACK_LAMBDA_IGNORE_ARCHITECTURE": 1,
        "LOCALSTACK_LAMBDA_KEEPALIVE_MS": 0,
    },
    tars = [
        ":local-init-resources.tar",
        ":local-handlers.tar",
    ],
)

oci_load(
    name = "local-load",
    image = ":local-image",
    repo_tags = [
        "bazel/subfootball-tracker-local:latest",
    ],
)

pkg_tar(
    name = "subfootball-website-stub-server.tar",
    srcs = [
        ":subfootball-website-stub-server_deploy.jar",
    ],
    package_dir = "/opt/code/subfootball-website-stub/",
)

oci_image(
    name = "subfootball-website-stub-image",
    base = "@temurin21jre//:temurin21jre",
    tars = [
        ":subfootball-website-stub-server.tar",
    ],
)

oci_load(
    name = "subfootball-website-stub-load",
    image = ":subfootball-website-stub-image",
    repo_tags = [
        "bazel/subfootball-website-stub:latest",
    ],
)

java_test_suite(
    name = "e2e-tests",
    size = "large",
    srcs = E2E_TESTS,
    env = {
        "AWS_ACCESS_KEY_ID": "fake",
        "AWS_SECRET_ACCESS_KEY": "fake",
        "AWS_REGION": "ap-southeast-2",
    },
    resources = [
        "src/test/resources/logback-test.xml",
    ],
    runner = "junit5",
    test_suffixes = ["E2ETest.java"],
    runtime_deps = JUNIT5_DEPS,
    deps = [
        ":lib",
        ":test-lib",
        "//lib/dynamodb:test-lib",
        "@maven//:ch_qos_logback_logback_classic",
        "@maven//:ch_qos_logback_logback_core",
        "@maven//:org_assertj_assertj_core",
        "@maven//:org_junit_jupiter_junit_jupiter_api",
        "@maven//:org_slf4j_slf4j_api",
        "@maven//:org_testcontainers_junit_jupiter",
        "@maven//:org_testcontainers_testcontainers",
        "@maven//:software_amazon_awssdk_dynamodb",
        "@maven//:software_amazon_awssdk_dynamodb_enhanced",
        "@maven//:software_amazon_awssdk_lambda",
        "@maven//:software_amazon_awssdk_sqs",
    ],
)
